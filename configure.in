# **************************************************************************
# SoGtk/configure.in
# $Id$

AC_INIT(src/Inventor/Gtk/SoGtk.h)
AC_CONFIG_AUX_DIR(cfg)

# If the Microsoft Visual C++ cl.exe compiler is available, set us up for
# compiling with it and to generate an MSWindows .dll file.

SIM_AC_SETUP_MSVC_IFELSE([
], [
  case $host in
  *-cygwin)
    AC_MSG_ERROR([You need Microsoft Visual C++ to build on Cygwin])
    ;;
  esac
])

# **************************************************************************
# Make sure the given source repository is in sync with the master source
# repository.

SIM_AC_CVS_CHANGES([
  # the moving of "conf-macros" to "cfg/m4"
  SIM_AC_CVS_CHANGE([
    rm -rf conf-macros
    cvs co SoGtkMacros
  ], [test -d cfg/m4/CVS], [test ! -d conf-macros])

  # the addition of the "data" module
  SIM_AC_CVS_CHANGE([
    cvs co SoGtkData
  ], [test -d data/CVS])
])

# **************************************************************************
# Library versioning

SOGTK_MAJOR_VERSION=0
SOGTK_MINOR_VERSION=9
SOGTK_MICRO_VERSION=99
SOGTK_VERSION=$SOGTK_MAJOR_VERSION.$SOGTK_MINOR_VERSION.$SOGTK_MICRO_VERSION
VERSION=$SOGTK_VERSION

AC_SUBST(SOGTK_MAJOR_VERSION)
AC_SUBST(SOGTK_MINOR_VERSION)
AC_SUBST(SOGTK_MICRO_VERSION)
AC_SUBST(SOGTK_VERSION)

# libtool versioning
LT_CURRENT=$SOGTK_MAJOR_VERSION
LT_AGE=0
LT_REVISION=`expr $SOGTK_MINOR_VERSION \* 100 + $SOGTK_MICRO_VERSION`

AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

# The following are set up in Inventor/Gtk/SoGtkBasic.h
AC_DEFINE_UNQUOTED([SOGTK_MAJOR_VERSION], [$SOGTK_MAJOR_VERSION],
  [The major version number for SoGtk])
AC_DEFINE_UNQUOTED([SOGTK_MINOR_VERSION], [$SOGTK_MINOR_VERSION],
  [The minor version number for SoGtk])
AC_DEFINE_UNQUOTED([SOGTK_MICRO_VERSION], [$SOGTK_MICRO_VERSION],
  [The micro version number for SoGtk])
AC_DEFINE_UNQUOTED([SOGTK_VERSION], ["$SOGTK_VERSION"],
  [The version string for SoGtk])

# **************************************************************************

AC_CANONICAL_SYSTEM

# **************************************************************************
# Locate compilers and set C++ as the default compiler to run in tests.

AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_LANG_CPLUSPLUS

# **************************************************************************
# Miscellaneous options and initializations.

AM_INIT_AUTOMAKE(SoGtk, $VERSION)

#  File to cram results from the configure tests into.
AM_CONFIG_HEADER(src/config.h)

#  Default to not building a static library.
#  Can be overridden by the user with --enable-static.
AM_DISABLE_STATIC

#  Turn off default maintainer make-rules -- use ./autogen.sh instead.
AM_MAINTAINER_MODE

# ranlib is a cygwin gcc tool, and we don't support cygwin gcc
if $BUILD_WITH_MSVC; then
  RANLIB=true
  export RANLIB
fi

# IRIX has 'ar' problems with libtool 1.3.5
case "$host_os" in
  irix*)
    if test x"${AR+set}" != x"set"; then
      AR="CC -ar"
      AR_FLAGS=
      export AR AR_FLAGS
      echo "overriding the libtool '\$AR' setting with 'CC -ar'"
    fi
    ;;
  *) ;;
esac

#  Initialize libtool
AC_PROG_LIBTOOL
AC_LANG(C)
AC_LANG(C++)

#  These are used for constructing the sogtk-config file.
SOGTK_EXTRA_CPPFLAGS=
SOGTK_EXTRA_LDFLAGS=
SOGTK_EXTRA_LIBS=
AC_SUBST(SOGTK_EXTRA_CPPFLAGS)
AC_SUBST(SOGTK_EXTRA_LDFLAGS)
AC_SUBST(SOGTK_EXTRA_LIBS)

SIM_EXPAND_DIR_VARS

AC_CHECK_HEADERS([windows.h dirent.h unistd.h])

# **************************************************************************
# If you just want to generate docs, ignore all the build-related tests

sim_ac_build_library=true
AC_ARG_ENABLE(
  [build],
  AC_HELP_STRING([--disable-build], [disable configuring for library build]), [
  case $enableval in
    no) sim_ac_build_library=false ;;
    *)  ;;
  esac])

if $sim_ac_build_library; then

# **************************************************************************

if test -n "$with_inventor" -a x"$with_inventor" != x"no"; then
  SIM_AC_CHECK_DL([
    SOGTK_EXTRA_CPPFLAGS="$SOGTK_EXTRA_CPPFLAGS $sim_ac_dl_cppflags"
    SOGTK_EXTRA_LDFLAGS="$SOGTK_EXTRA_LDFLAGS $sim_ac_dl_ldflags"
    SOGTK_EXTRA_LIBS="$sim_ac_dl_libs $SOGTK_EXTRA_LIBS"
  ])

  SIM_AC_CHECK_X11([
    SOGTK_EXTRA_CPPFLAGS="$SOGTK_EXTRA_CPPFLAGS $sim_ac_x11_cppflags"
    SOGTK_EXTRA_LDFLAGS="$SOGTK_EXTRA_LDFLAGS $sim_ac_x11_ldflags"
    SOGTK_EXTRA_LIBS="$sim_ac_x11_libs $SOGTK_EXTRA_LIBS"

    SIM_AC_CHECK_X11SHMEM([
      SOGTK_EXTRA_CPPFLAGS="$SOGTK_EXTRA_CPPFLAGS $sim_ac_x11shmem_cppflags"
      SOGTK_EXTRA_LDFLAGS="$SOGTK_EXTRA_LDFLAGS $sim_ac_x11shmem_ldflags"
      SOGTK_EXTRA_LIBS="$sim_ac_x11shmem_libs $SOGTK_EXTRA_LIBS"
    ], [
      AC_MSG_WARN([couldn't use X11 shared memory extension - may cause problems with OpenGL linking later])
    ])
  ], [
    AC_MSG_WARN([couldn't use X11 library - may cause problems with OpenGL linking later])
  ])

  SIM_AC_CHECK_OPENGL([
    SOGTK_EXTRA_CPPFLAGS="$SOGTK_EXTRA_CPPFLAGS $sim_ac_gl_cppflags"
    SOGTK_EXTRA_CFLAGS="$SOGTK_EXTRA_CFLAGS $sim_ac_gl_cflags"
    SOGTK_EXTRA_CXXFLAGS="$SOGTK_EXTRA_CXXFLAGS $sim_ac_gl_cxxflags"
    SOGTK_EXTRA_LDFLAGS="$SOGTK_EXTRA_LDFLAGS $sim_ac_gl_ldflags"
    SOGTK_EXTRA_LIBS="$sim_ac_gl_libs $SOGTK_EXTRA_LIBS"
    CPPFLAGS="$CPPFLAGS $sim_ac_gl_cppflags"
    CFLAGS="$CFLAGS $sim_ac_gl_cflags"
    CXXFLAGS="$CXXFLAGS $sim_ac_gl_cxxflags"
    LDFLAGS="$LDFLAGS $sim_ac_gl_ldflags"
    LIBS="$sim_ac_gl_libs $LIBS"
  ], [AC_MSG_ERROR([need OpenGL-compatible development system installation])])

  SIM_AC_CHECK_GLU([
    SOGTK_EXTRA_CPPFLAGS="$SOGTK_EXTRA_CPPFLAGS $sim_ac_glu_cppflags"
    SOGTK_EXTRA_LDFLAGS="$SOGTK_EXTRA_LDFLAGS $sim_ac_glu_ldflags"
    SOGTK_EXTRA_LIBS="$sim_ac_glu_libs $SOGTK_EXTRA_LIBS"
  ], [
    AC_MSG_ERROR([need OpenGL utility library])
  ])

  SIM_AC_HAVE_INVENTOR_IFELSE([
    SOGTK_EXTRA_CPPFLAGS="$SOGTK_EXTRA_CPPFLAGS $sim_ac_inventor_cppflags"
    SOGTK_EXTRA_LDFLAGS="$SOGTK_EXTRA_LDFLAGS $sim_ac_inventor_ldflags"
    SOGTK_EXTRA_LIBS="$sim_ac_inventor_libs $SOGTK_EXTRA_LIBS"
    CPPFLAGS="$CPPFLAGS $sim_ac_inventor_cppflags"
    LDFLAGS="$LDFLAGS $sim_ac_inventor_ldflags"
    LIBS="$sim_ac_inventor_libs $LIBS"
  ], [
    AC_MSG_ERROR([couldn't compile and link against Open Inventor])
  ])
  SIM_AC_CONFIGURATION_SETTING([Open Inventor brand], [SGI/TGS Inventor])
else
  SIM_AC_HAVE_COIN_IFELSE([
    CPPFLAGS="$CPPFLAGS $sim_ac_coin_cppflags"
    LDFLAGS="$CPPFLAGS $sim_ac_coin_ldflags"
    LIBS="$sim_ac_coin_libs $LIBS"
    SOGTK_EXTRA_CPPFLAGS="$SOGTK_EXTRA_CPPFLAGS $sim_ac_coin_cppflags"
    SOGTK_EXTRA_LDFLAGS="$SOGTK_EXTRA_LDFLAGS $sim_ac_coin_ldflags"
    SOGTK_EXTRA_LIBS="$sim_ac_coin_libs $SOGTK_EXTRA_LIBS"
  ], [
    AC_MSG_ERROR([couldn't compile and link against Coin])
  ])
  SIM_AC_CONFIGURATION_SETTING([Open Inventor brand], [Coin])
fi

# **************************************************************************
# Check for miscellaneous implementation-dependent Open Inventor features

SIM_AC_HAVE_INVENTOR_NODE(SoPolygonOffset)

SIM_AC_HAVE_INVENTOR_FEATURE(
  [for SoKeyboardEvent::DELETE enum],
  [#include <Inventor/events/SoKeyboardEvent.h>], [
   /* This is either DELETE or KEY_DELETE */
   SoKeyboardEvent::Key key = SoKeyboardEvent::DELETE;],
  [HAVE_SOKEYBOARDEVENT_DELETE])

SIM_AC_HAVE_INVENTOR_FEATURE(
  [for SoMouseButtonEvent::BUTTON5 enum],
  [#include <Inventor/events/SoMouseButtonEvent.h>], [
   /* mouse wheel support needs BUTTON4 and BUTTON5 */
   SoMouseButtonEvent::Button button = SoMouseButtonEvent::BUTTON5;],
  [HAVE_SOMOUSEBUTTONEVENT_BUTTON5])

SIM_AC_HAVE_INVENTOR_FEATURE(
  [for SoCamera::setStereoMode() method],
  [#include <Inventor/nodes/SoPerspectiveCamera.h>], [
   /* SoCamera is abstract, so test with SoPerspectiveCamera */
   SoPerspectiveCamera * c = new SoPerspectiveCamera;
   c->setStereoMode(SoCamera::MONOSCOPIC);],
  [HAVE_SOCAMERA_SETSTEREOMODE])

# **************************************************************************
# Check for GTK+

# Ugly AM_PATH_GTK check macro from gtk.m4 doesn't work with C++ compilers.
sim_ac_gtk_store_ldflags=$LDFLAGS
LDFLAGS=
sim_ac_gtk_store_libs=$LIBS
LIBS=

AC_LANG_PUSH(C)
AM_PATH_GTK(1.2.6,
  [SOGTK_EXTRA_CPPFLAGS="$GTK_CFLAGS $SOGTK_EXTRA_CPPFLAGS"
   CPPFLAGS="$GTK_CFLAGS $CPPFLAGS"],
  AC_MSG_ERROR(Test for Gtk+ failed.))
AC_LANG_POP

LDFLAGS=$sim_ac_gtk_store_ldflags
LIBS=$sim_ac_gtk_store_libs

# The AM_PATH_GTK check macro just lumps the LDFLAGS and LIBS together
# in GTK_LIBS.
for i in $GTK_LIBS; do
  if test -n "`echo $i | egrep '^-L'`"; then
    LDFLAGS="$LDFLAGS $i"
    SOGTK_EXTRA_LDFLAGS="$SOGTK_EXTRA_LDFLAGS $i"
  else
    LIBS="$LIBS $i"
    SOGTK_EXTRA_LIBS="$SOGTK_EXTRA_LIBS $i"
  fi
done

# Check for GtkGLArea

LIBS="-lgtkgl $LIBS"
AC_CACHE_CHECK([whether the GtkGLArea widget is available],
  sim_cv_lib_gtkgl,
  [AC_TRY_LINK([#include <gtkgl/gtkglarea.h>],
               [gtk_gl_area_new(0L);],
               sim_cv_lib_gtkgl=yes,
               sim_cv_lib_gtkgl=no)])

if test x"$sim_cv_lib_gtkgl" != xyes; then
  AC_MSG_ERROR(Test for GtkGLArea extensions widget failed.)
else
  SOGTK_EXTRA_LIBS="-lgtkgl $SOGTK_EXTRA_LIBS"
fi

# **************************************************************************
# Compiler control.

SIM_AC_COMPILE_DEBUG([
  CPPFLAGS="$CPPFLAGS -DSOGTK_DEBUG=1"
#   SIM_AC_CONFIGURATION_WARNING(
#     [debug compilation causes slower and larger executabled])
], [
  CPPFLAGS="$CPPFLAGS -DSOGTK_DEBUG=0"
])
CPPFLAGS="$CPPFLAGS -DSOGTK_INTERNAL"

SIM_AC_DEBUGSYMBOLS
SIM_AC_RTTI_SUPPORT
SIM_EXCEPTION_HANDLING
SIM_PROFILING_SUPPORT
SIM_COMPILER_WARNINGS

# **************************************************************************
# Do we want to compile the example programs?

# * --with-test-code is a silent option
AM_CONDITIONAL(BUILD_TESTS, test "x${with_test_code+set}" = "xset")

SIM_AC_SOGUI_STATIC_DEFAULTS

if $sim_ac_static_defaults; then
  SIM_AC_CONFIGURATION_SETTING([Static Materials], [Yes])
else
  SIM_AC_CONFIGURATION_SETTING([Static Materials], [No])
fi

# **************************************************************************
# Gettext support

## Add the languages which your application supports here.
ALL_LINGUAS="de no"
SIM_GNU_GETTEXT 
## Set PACKAGE_LOCALE_DIR in config.h.
if test x"${prefix}" = x"NONE"; then  
  package_local_dir="${ac_default_prefix}/${DATADIRNAME}/locale";
else
  package_locale_dir="${prefix}/${DATADIRNAME}/locale";
fi
AC_DEFINE_UNQUOTED([PACKAGE_LOCALE_DIR],
    ["${package_locale_dir}"],
    [Location the locale data will get installed to.])

AM_CONDITIONAL([BUILD_INTL], test x"$GMSGFMT" = x"")

# **************************************************************************
# Submodules that must be configured.

AC_CONFIG_SUBDIRS(data)

# **************************************************************************
# FIXME: avoid the shift magic

# this will depend on platform in a while (Window will use sogtk$major)
SOGTK_LIBNAME=-lSoGtk

SIM_AC_UNIQIFY_LIST(SOGTK_EXTRA_CPPFLAGS, -I$includedir $SOGTK_EXTRA_CPPFLAGS)
set : $SOGTK_EXTRA_CPPFLAGS
shift
shift
SOGTK_EXTRA_CPPFLAGS=$@

SIM_AC_UNIQIFY_LIST(SOGTK_EXTRA_LDFLAGS, -L$libdir $SOGTK_EXTRA_LDFLAGS)
set : $SOGTK_EXTRA_LDFLAGS
shift
shift
SOGTK_EXTRA_LDFLAGS=$@

SIM_AC_UNIQIFY_LIST(SOGTK_EXTRA_LIBS, $SOGTK_LIBNAME $SOGTK_EXTRA_LIBS)
set : $SOGTK_EXTRA_LIBS
shift
shift
SOGTK_EXTRA_LIBS=$@

SIM_AC_UNIQIFY_LIST(CPPFLAGS, $CPPFLAGS)
SIM_AC_UNIQIFY_LIST(LDFLAGS, $LDFLAGS)
SIM_AC_UNIQIFY_LIST(LIBS, $LIBS)

# **************************************************************************
# END OF "if $sim_ac_build_library" TEST

fi

# **************************************************************************
# Variable substitutions for using libSoGtk in the generic code.

GUI=GTK
Gui=Gtk
gui=gtk
WIDGET="GtkWidget *"
EVENT="GdkEvent *"
COMPONENTHEADER=""

AC_SUBST(GUI)
AC_SUBST(Gui)
AC_SUBST(gui)
AC_SUBST(EVENT)
AC_SUBST(WIDGET)
AC_SUBST(COMPONENTHEADER)

# **************************************************************************
# Section for Doxygen

AC_ARG_WITH(
  html,
  AC_HELP_STRING([--with-html], [build and install SoGtk HTML documentation]),
  [case "${withval}" in
    yes) want_html=yes ;;
    no)  want_html=no ;;
    *) AC_MSG_ERROR(bad value "${withval}" for --with-html) ;;
  esac],
  [want_html=no])

AC_ARG_WITH(
  man,
  AC_HELP_STRING([--with-man], [build and install SoGtk man pages]),
  [case "${withval}" in
    yes) want_man=yes ;;
    no)  want_man=no ;;
    *) AC_MSG_ERROR(bad value "${withval}" for --with-man) ;;
  esac],
  [want_man=no])

SOGTK_DOC_HTML=`echo $want_html | tr a-z A-Z`
SOGTK_DOC_MAN=`echo $want_man | tr a-z A-Z`
AC_SUBST(SOGTK_DOC_HTML)
AC_SUBST(SOGTK_DOC_MAN)

sogtk_build_dir=`pwd`
sogtk_src_dir=`cd $srcdir; pwd`

AC_SUBST(sogtk_src_dir)
AC_SUBST(sogtk_build_dir)

if test x"$want_html" != xno -o x"$want_man" != xno; then
  SIM_AC_DOXYGEN_TOOL(,
    AC_MSG_ERROR(Could not find the doxygen tool -- see http://www.stack.nl/~dimitri/doxygen/))
  # sim_ac_doxygen_our_version=1.1.5
  # if test x"$sim_ac_doxygen_avail" != x"$sim_ac_doxygen_our_version"; then
  #   AC_MSG_WARN(You are not using Doxygen version $sim_ac_doxygen_our_version)
  # fi
  AC_SUBST(sim_ac_doxygen_exe)

  AC_PATH_PROG(sim_ac_perl_exe, perl, false, $PATH)
  if test x"$sim_ac_perl_exe" = xfalse; then
    AC_MSG_WARN(Could not find the Perl executable)
  fi
  AC_SUBST(sim_ac_perl_exe)

  HTML_DOC_DIR=`eval echo "$datadir/SoGtk/html"`
  AC_SUBST(HTML_DOC_DIR)
fi

SIM_AC_CONFIGURATION_SETTING([Install Prefix], [$prefix])

# **************************************************************************
# List all makefiles configure must automatically generate.

AC_OUTPUT([
	sogtk-config
	Makefile
	src/Inventor/Gtk/Makefile
	src/Inventor/Gtk/devices/Makefile
	src/Inventor/Gtk/viewers/Makefile
	src/Inventor/Gtk/widgets/Makefile
	po/Makefile.in
	intl/Makefile
        build/Makefile
        build/sogtk.doxygen
        build/sogtk.spec
	test-code/Makefile
	test-code/components/Makefile
	test-code/widgets/Makefile
], [
	chmod a+x sogtk-config
])

SIM_AC_CONFIGURATION_SUMMARY

echo ""
echo "Now, run 'make install' to build and install So$Gui."
echo ""

# **************************************************************************
